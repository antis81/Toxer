PROJECT (Toxer)
CMAKE_MINIMUM_REQUIRED (VERSION 3.5)

IF (NOT CMAKE_BUILD_TYPE)
  SET(CMAKE_BUILD_TYPE Release)
ENDIF()

SET (PROJECT_VERSION_MAJOR 1)
SET (PROJECT_VERSION_MINOR 0)
SET (PROJECT_VERSION_PATCH 0)
SET (PROJECT_VERSION_TWEAK "pre-alpha")
SET (PROJECT_VERSION
    "${PROJECT_VERSION_MAJOR}.${PROJECT_VERSION_MINOR}.${PROJECT_VERSION_PATCH}"
    )
IF (NOT PROJECT_VERSION_TWEAK STREQUAL "")
SET(PROJECT_VERSION "${PROJECT_VERSION}-${PROJECT_VERSION_TWEAK}")
ENDIF()
ADD_DEFINITIONS (-DTOXER_VERSION="${PROJECT_VERSION}")

OPTION (TOXER_STATIC "Link Toxer to static libraries." OFF)

SET (CMAKE_MODULE_PATH ${PROJECT_SOURCE_DIR}/cmake;${CMAKE_MODULE_PATH})
SET (CMAKE_CXX_STANDARD 11)
SET (CMAKE_CXX_STANDARD_REQUIRED ON)

FIND_PACKAGE (Qt5 COMPONENTS Core Network Qml Gui Quick REQUIRED)
FIND_PACKAGE (sodium REQUIRED)

FILE (GLOB_RECURSE SCRIPTS "scripts/*.*")
ADD_CUSTOM_TARGET(scripts SOURCES ${SCRIPTS})

QT5_ADD_RESOURCES (RESOURCES res/res.qrc qml/qml.qrc)

SET (CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -fno-exceptions -fno-rtti")
SET (CMAKE_AUTOMOC ON)

ADD_SUBDIRECTORY (lib)

SET (TOX_LIBS
    toxencryptsave
    toxcore
    )

IF (TOXER_STATIC)
    FIND_PACKAGE (Threads REQUIRED)

    SET (CMAKE_CXX_FLAGS "-pthread ${CMAKE_CXX_FLAGS}")
    SET (CMAKE_EXE_LINKER_FLAGS "-static ${CMAKE_EXE_LINKER_FLAGS}")

    SET (sodium_USE_STATIC_LIBS ON)

    INCLUDE (Qt5Static)

    SET (TOX_LIBS
        "${TOX_LIBS}"
        toxdns
        toxav
        )
ENDIF ()

SET (SRC_FILES ${SRC_FILES}
    src/IToxNotify.cpp
    src/main.cpp
    src/Private/ToxBootstrap.cpp
    src/Private/ToxerPrivate.cpp
    src/Private/ToxProfile.cpp
    src/Settings.cpp
    src/Toxer.cpp
    src/ToxTypes.cpp
    )

FILE (GLOB_RECURSE JS_FILES "qml/*.js")
ADD_CUSTOM_TARGET (js_files SOURCES ${JS_FILES})

FILE (GLOB_RECURSE QML_FILES "qml/*.qml")
ADD_CUSTOM_TARGET (qml_files SOURCES ${QML_FILES})

ADD_EXECUTABLE (toxer
    ${SRC_FILES}
    ${RESOURCES}
    )

SET_PROPERTY (TARGET toxer APPEND PROPERTY COMPILE_DEFINITIONS
    QT_NO_CAST_FROM_ASCII
    QT_NO_CAST_TO_ASCII
    QT_NO_CAST_FROM_BYTEARRAY
    )

TARGET_INCLUDE_DIRECTORIES (toxer PRIVATE
    $<BUILD_INTERFACE:
    ${CMAKE_CURRENT_SOURCE_DIR}/src
    >)

TARGET_LINK_LIBRARIES (toxer
    Qt5::Quick

    ${TOX_LIBS}
    sodium

    ${CMAKE_THREAD_LIBS_INIT}
    )

INSTALL (TARGETS
    toxer
    RUNTIME DESTINATION bin COMPONENT Runtime
    LIBRARY DESTINATION lib COMPONENT Runtime
    )
